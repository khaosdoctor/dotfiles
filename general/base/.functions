#!/bin/zsh
#CUSTOM FUNCTIONS
cd() {
    if builtin cd "$@"; then
        eza --icons --color=auto -a
    else
        return $?
    fi
}

cat() {
  # Checks which text renderer to use depending on the installed programs
  local text_cat
  if command -v bat >/dev/null 2>&1; then
    text_cat=(/usr/bin/bat --paging=never)
  else
    text_cat=(/usr/bin/cat)
  fi

  # If it's not a TTY behaves like CAT
  if [[ ! -t 0 ]]; then
    command "${text_cat[@]}" "$@"
    return
  fi

  # If we have kitty then icat is available
  local has_kitty=false
  [[ -n "$KITTY_WINDOW_ID" ]] && has_kitty=true

  # If there are any flags present defer to the text renderer
  # if any arg is not a regular file, also defer to the renderer
  for arg in "$@"; do
    [[ "$arg" == -* ]] && command "${text_cat[@]}" "$@" && return
    [[ ! -f "$arg" ]] && command "${text_cat[@]}" "$@" && return
  done

  # Checks if any of the files is an image, if so, uses icat if in kitty
  for file in "$@"; do
    local mime
    mime=$(file --mime-type -b "$file")

    if $has_kitty && [[ "$mime" == image/* ]]; then
      kitten icat "$file"
    else
      command "${text_cat[@]}" "$file"
    fi
  done
}

git_goto() {
    find . -name .git -type d -execdir git checkout "$1" ";"
}

mkcd() {
    mkdir -p "$1" && cd "$1" || return
}

whichport() {
    sudo lsof -i -n -P | grep :$1
}

# requires yazi, allows to return to the last yazi folder when exiting
function yy() {
    local tmp="$(mktemp -t "yazi-cwd.XXXXXX")"
    yazi "$@" --cwd-file="$tmp"
    if cwd="$(cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
        cd -- "$cwd" || return
    fi
    rm -f -- "$tmp"
}

if [[ "$(uname -s)" == "Linux" ]]; then
    function create-tui-desktop() {
        APP=$1
        TERMINAL=${2:-rio}
        SRC_PATH=$(find /usr/share/applications ~/.local/share/applications -name "${APP}.desktop" | head -n1)

        if [ -z "$SRC_PATH" ]; then
            echo "âŒ .desktop file for ${APP} not found!"
            exit 1
        fi

        DEST_PATH="$HOME/.local/share/applications/${APP}.desktop"
        echo "Copying and creating override for ${APP} in ${DEST_PATH}"
        cp "$SRC_PATH" "$DEST_PATH"

        sed -i "s|^Exec=.*|Exec=${TERMINAL} -e ${APP}|" "$DEST_PATH"
        sed -i "s|^Terminal=.*|Terminal=false|" "$DEST_PATH"
        update-desktop-database ~/.local/share/applications
    }
fi

function systemLog() {
    local input

    if [ ! -t 0 ]; then
        input=$(cat)
    else
        input=$1
    fi

    # on mac log to console
    if [[ "$(uname -o)" == "Darwin" ]]; then
        if [ -x "$(command -v logger)" ]; then
            logger -t "${2:-'UserLogger'}" "[UserTerminalLogger] $input" &
        else
            echo "[${2:-"INFO"}] $input" >>/var/log/user_terminal.log
        fi
    # Linux with systemd log to journal
    elif [[ "$(uname -s)" == "Linux" ]]; then
        if [ -x "$(command -v systemd-cat)" ]; then
            command -v systemd-cat && (echo "$1" | systemd-cat -t "${2:-"info"}") &
        else
            echo "[${2:-"INFO"}] $input" >>/var/log/user_terminal.log
        fi
    # default to log to file
    else
        echo "[${2:-"INFO"}] $input" >>/var/log/user_terminal.log
    fi
}

if [ -x "$(command -v brew)" ]; then
    brew() {
        "$HOMEBREW_PREFIX/bin/brew" "$@"
        local command=$1
        if [ "$command" = "install" ] || [ "$command" = "uninstall" ]; then
            echo "Detected $command command, updating Brewfile... Don't forget to commit the changes!"
            shift
            systemLog "Updating brewfile after $command command with options: $*"
            "$HOMEBREW_PREFIX/bin/brew" bundle dump -f -a --no-upgrade --file=- >"$HOME/Brewfile"
        fi
    }
fi
